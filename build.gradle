plugins {
  alias(deps.plugins.kotest) apply false
  alias(deps.plugins.spotless)
  alias(deps.plugins.kotlinx.kover)
  alias(deps.plugins.kotlinx.serialization) apply false
}

apply plugin: 'kotlinx-atomicfu'

spotless {
  groovyGradle {
    target "**/*.gradle"
    trimTrailingWhitespace()
    endWithNewline()
    greclipse().configFile('./greclipse.properties')
  }
  kotlin {
    target "**/*.kt"
    targetExclude "buildSrc/build/"
    ktlint(deps.versions.ktlint.get()).editorConfigOverride([
      "indent_size"             : "2",
      "max_line_length"         : "80",
      "indent_style"            : "space",
      "charset"                 : "utf-8",
      "end_of_line"             : "lf",
      "disabled_rules"          : "filename",
      "trim_trailing_whitespace": "true",
      "insert_final_newline"    : "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
  kotlinGradle {
    ktlint()
  }
}

tasks.koverMergedHtmlReport {
  enabled = true
  htmlReportDir.set(
    layout.buildDirectory.dir("reports/kover/merged-report/html-result"))
}

tasks.koverMergedXmlReport {
  enabled = true
  xmlReportFile.set(
    layout.buildDirectory.file("reports/kover/merged-report/result.xml"))
}

tasks.koverCollectReports {
  outputDir.set(layout.buildDirectory.dir("reports/kover/all-modules-reports"))
}
